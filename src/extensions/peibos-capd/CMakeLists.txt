# ==================================================================
#  Codac - cmake configuration file
# ==================================================================

list(APPEND CODAC_PEIBOS_CAPD_SRC
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_peibos_capd.cpp
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_peibos_capd.h
)

################################################################################
# Create the target for libcodac-peibos-capd
################################################################################

  #if(NOT CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 20)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
  #endif()

  add_library(${PROJECT_NAME}-peibos-capd ${CODAC_PEIBOS_CAPD_SRC})
  target_include_directories(${PROJECT_NAME}-peibos-capd PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/../capd
  )

  target_link_libraries(${PROJECT_NAME}-peibos-capd PUBLIC ${PROJECT_NAME}-core ${PROJECT_NAME}-capd Ibex::ibex Eigen3::Eigen capd::capd)  

  ################################################################################
  # For the generation of the PKG file
  ################################################################################

  set(CODAC_PKG_CONFIG_CFLAGS "${CODAC_PKG_CONFIG_CFLAGS} -I\${includedir}/${PROJECT_NAME}-peibos-capd" PARENT_SCOPE)
  set(CODAC_PKG_CONFIG_LIBS "${CODAC_PKG_CONFIG_LIBS} -l${PROJECT_NAME}-peibos-capd" PARENT_SCOPE)


  ################################################################################
  # Installation of libcodac-peibos-capd files
  ################################################################################

  # Getting header files from sources

  foreach(srcfile ${CODAC_PEIBOS_CAPD_SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND CODAC_PEIBOS_CAPD_HDR ${srcfile})
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
    endif()
  endforeach()

  # Generating the file codac-peibos-capd.h

  set(CODAC_PEIBOS_CAPD_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-peibos-capd.h)
  file(WRITE ${CODAC_PEIBOS_CAPD_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_PEIBOS_CAPD_MAIN_HEADER} "#pragma once\n\n")
  foreach(header_path ${CODAC_PEIBOS_CAPD_HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_PEIBOS_CAPD_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(COPY ${CODAC_PEIBOS_CAPD_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)

  # Install files in system directories

  install(TARGETS ${PROJECT_NAME}-peibos-capd DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${CODAC_PEIBOS_CAPD_HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-peibos-capd)
  install(FILES ${CODAC_PEIBOS_CAPD_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-peibos-capd)