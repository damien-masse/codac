# ==================================================================
#  Codac - cmake configuration file
# ==================================================================

list(APPEND CODAC_CAPD_SRC
    
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_capd.cpp
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_capd.h
)

################################################################################
# Create the target for libcodac-capd
################################################################################

  #if(NOT CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 20)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
  #endif()

  add_library(${PROJECT_NAME}-capd ${CODAC_CAPD_SRC})
  target_link_libraries(${PROJECT_NAME}-capd PUBLIC ${PROJECT_NAME}-core Ibex::ibex Eigen3::Eigen capd::capd)


  ################################################################################
  # For the generation of the PKG file
  ################################################################################

  set(CODAC_PKG_CONFIG_CFLAGS "${CODAC_PKG_CONFIG_CFLAGS} -I\${includedir}/${PROJECT_NAME}-capd" PARENT_SCOPE)
  set(CODAC_PKG_CONFIG_LIBS "${CODAC_PKG_CONFIG_LIBS} -l${PROJECT_NAME}-capd" PARENT_SCOPE)


  ################################################################################
  # Installation of libcodac-capd files
  ################################################################################

  # Getting header files from sources

  foreach(srcfile ${CODAC_CAPD_SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND CODAC_CAPD_HDR ${srcfile})
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
    endif()
  endforeach()

  # Generating the file codac-capd.h

  set(CODAC_CAPD_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-capd.h)
  file(WRITE ${CODAC_CAPD_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_CAPD_MAIN_HEADER} "#pragma once\n\n")
  foreach(header_path ${CODAC_CAPD_HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_CAPD_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(COPY ${CODAC_CAPD_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)

  # Install files in system directories

  install(TARGETS ${PROJECT_NAME}-capd DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${CODAC_CAPD_HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-capd)
  install(FILES ${CODAC_CAPD_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-capd)