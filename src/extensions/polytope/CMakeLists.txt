# ==================================================================
#  Codac - cmake configuration file
# ==================================================================

list(APPEND CODAC_POLYTOPE_SRC
    
  ${CMAKE_CURRENT_SOURCE_DIR}/clp/codac2_clp.cpp
  ${CMAKE_CURRENT_SOURCE_DIR}/clp/codac2_clp.h
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_Facet.h
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_Facet.cpp
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_dd.h
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_dd.cpp
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_Polytope.h
  ${CMAKE_CURRENT_SOURCE_DIR}/codac2_Polytope.cpp
)

################################################################################
# Create the target for libcodac-polytope
################################################################################

  #if(NOT CMAKE_CXX_STANDARD)
    set(CMAKE_CXX_STANDARD 20)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
  #endif()

  add_library(${PROJECT_NAME}-polytope ${CODAC_POLYTOPE_SRC})
  target_include_directories(${PROJECT_NAME}-polytope PUBLIC
    ${CLP_INCLUDE_DIRS}
    ${CMAKE_CURRENT_SOURCE_DIR}/
    ${CMAKE_CURRENT_SOURCE_DIR}/clp
  )
  target_link_libraries(${PROJECT_NAME}-polytope PUBLIC ${PROJECT_NAME}-core Ibex::ibex Eigen3::Eigen ${CLP_LIBRARIES})


  ################################################################################
  # For the generation of the PKG file
  ################################################################################

  set(CODAC_PKG_CONFIG_CFLAGS "${CODAC_PKG_CONFIG_CFLAGS} -I\${includedir}/${PROJECT_NAME}-polytope" PARENT_SCOPE)
  set(CODAC_PKG_CONFIG_LIBS "${CODAC_PKG_CONFIG_LIBS} -l${PROJECT_NAME}-polytope" PARENT_SCOPE)


  ################################################################################
  # Installation of libcodac-polytope files
  ################################################################################

  # Getting header files from sources

  foreach(srcfile ${CODAC_POLYTOPE_SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND CODAC_POLYTOPE_HDR ${srcfile})
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
    endif()
  endforeach()

  # Generating the file codac-polytope.h

  set(CODAC_POLYTOPE_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-polytope.h)
  file(WRITE ${CODAC_POLYTOPE_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_POLYTOPE_MAIN_HEADER} "#pragma once\n\n")
  foreach(header_path ${CODAC_POLYTOPE_HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_POLYTOPE_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(COPY ${CODAC_POLYTOPE_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)

  # Install files in system directories

  install(TARGETS ${PROJECT_NAME}-polytope DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${CODAC_POLYTOPE_HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-polytope)
  install(FILES ${CODAC_POLYTOPE_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}-polytope)
