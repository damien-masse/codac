# ==================================================================
#  codac-capd - cmake configuration file
# ==================================================================


list(APPEND SRC ${CMAKE_CURRENT_SOURCE_DIR}/codac_capd_integrateODE.h
                ${CMAKE_CURRENT_SOURCE_DIR}/codac_capd_integrateODE.cpp
                ${CMAKE_CURRENT_SOURCE_DIR}/codac_capd_helpers.h
                ${CMAKE_CURRENT_SOURCE_DIR}/codac_capd_helpers.cpp)

################################################################################
# Create the target for libcodac-capd
################################################################################

  add_library(codac-capd ${SRC})
  target_compile_options(codac-capd PUBLIC ${CODAC_CFLAGS})

  # todo: find a clean way to access codac header files?
  set(CODAC_HEADERS_DIR ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
  target_include_directories(codac-capd PUBLIC ${CODAC_HEADERS_DIR}
                                               ${CMAKE_CURRENT_SOURCE_DIR})
  target_link_libraries(codac-capd PUBLIC Ibex::ibex codac)


################################################################################
# Installation of libcodac-capd files
################################################################################

# Getting header files from sources

  foreach(srcfile ${SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND HDR ${srcfile})
      # Copying header files for other Codac libraries compiled in the same time
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
    endif()
  endforeach()

# Generates a codac_capd.h file

  set(CODAC_3RD_CAPD_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-capd.h)
  file(WRITE ${CODAC_3RD_CAPD_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_3RD_CAPD_MAIN_HEADER} "#ifndef __CODAC_3RD_CAPD_H__\n#define __CODAC_3RD_CAPD_H__\n\n")
  foreach(header_path ${HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_3RD_CAPD_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(APPEND ${CODAC_3RD_CAPD_MAIN_HEADER} "\n#endif \n")
  file(COPY ${CODAC_3RD_CAPD_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)

# Install files in system directories
install(TARGETS codac-capd DESTINATION ${CMAKE_INSTALL_LIBDIR})
install(FILES ${HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/codac-capd)
install(FILES ${CODAC_3RD_CAPD_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/codac-capd)