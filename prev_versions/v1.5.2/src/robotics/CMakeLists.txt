# ==================================================================
#  codac-rob - cmake configuration file
# ==================================================================

  list(APPEND SRC ${CMAKE_CURRENT_SOURCE_DIR}/graphics/codac_VIBesFigMap.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/graphics/codac_VIBesFigMap.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/objects/codac_Beacon.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/objects/codac_Beacon.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/objects/codac_Wall.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/objects/codac_Wall.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/loops/codac_TPlane.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/loops/codac_TPlane.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/codac_DataLoaderLissajous.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/codac_DataLoaderRedermor.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/codac_DataLoader.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/codac_DataLoaderRedermor.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/codac_DataLoader.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/codac_DataLoaderLissajous.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/contractors/codac_CtcConstell.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/contractors/codac_CtcConstell.h
                  )


################################################################################
# Create the target for libcodac-rob
################################################################################

  add_library(codac-rob ${SRC})
  target_compile_options(codac-rob PUBLIC ${CODAC_CFLAGS})

  # todo: find a clean way to access codac header files?
  set(CODAC_HEADERS_DIR ${CMAKE_CURRENT_BINARY_DIR}/../../include)
  target_include_directories(codac-rob PUBLIC ${CODAC_HEADERS_DIR}
                                              ${CMAKE_CURRENT_SOURCE_DIR}/graphics
                                              ${CMAKE_CURRENT_SOURCE_DIR}/objects
                                              ${CMAKE_CURRENT_SOURCE_DIR}/loops
                                              ${CMAKE_CURRENT_SOURCE_DIR}/data
                                              ${CMAKE_CURRENT_SOURCE_DIR}/contractors)
  target_link_libraries(codac-rob PUBLIC Ibex::ibex codac)


################################################################################
# Installation of libcodac-rob files
################################################################################

# Getting header files from sources

  foreach(srcfile ${SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND HDR ${srcfile})
      # Copying header files for other Codac libraries compiled in the same time
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)
    endif()
  endforeach()

# Generating a codac-rob.h file

  set(CODAC_ROB_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/codac-rob.h)
  file(WRITE ${CODAC_ROB_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${CODAC_ROB_MAIN_HEADER} "#ifndef __CODAC_ROB_H__\n#define __CODAC_ROB_H__\n\n")
  foreach(header_path ${HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${CODAC_ROB_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(APPEND ${CODAC_ROB_MAIN_HEADER} "\n#endif /* __CODAC_ROB_H__ */\n")
  file(COPY ${CODAC_ROB_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)

# Install files in system directories

  install(TARGETS codac-rob DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/codac-rob)
  install(FILES ${CODAC_ROB_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/codac-rob)